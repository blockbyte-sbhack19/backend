buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordformation:${corda_gradle_plugins_version}"
    }
}

def RUN_CORDA_GROUP = "corda"

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordformation'

task deployNodes(type: net.corda.plugins.Cordform, group: RUN_CORDA_GROUP) {
    dependsOn 'cordapp:jar'
    directory "$rootDir/build/nodes"

    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating: false]
        p2pPort 10001
        rpcSettings {
            address("localhost:10002")
            adminAddress("localhost:10003")
        }
        cordapps = ["com.blockbyte.poc.theLand:cordapp:0.1"]
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }

    node {
        name "O=Lender,L=London,C=GB"
        p2pPort 10101
        rpcSettings {
            address("localhost:10102")
            adminAddress("localhost:10103")
        }
        cordapps = ["com.blockbyte.poc.theLand:cordapp:0.1"]
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }

    node {
        name "O=Leaser,L=London,C=GB"
        p2pPort 10201
        rpcSettings {
            address("localhost:10202")
            adminAddress("localhost:10203")
        }
        cordapps = ["com.blockbyte.poc.theLand:cordapp:0.1"]
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }

    node {
        name "O=Provider,L=London,C=GB"
        p2pPort 10301
        rpcSettings {
            address("localhost:10302")
            adminAddress("localhost:10303")
        }
        cordapps = ["com.blockbyte.poc.theLand:cordapp:0.1"]
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}


deployNodes.initializeConfiguration()
def cordaNodes = deployNodes.nodes


configurations {
    /** Dependencies that must be deployed into running Corda nodes */
    cordappDeploy
}

task deployJarsToNodes(group: RUN_CORDA_GROUP) {
    doLast {
        def cordaDeployConfiguration = configurations.cordappDeploy
        def cordaDeployDependencies = cordaDeployConfiguration.resolvedConfiguration.firstLevelModuleDependencies
        def cordaDeployArtifacts = cordaDeployDependencies.collectMany { it.moduleArtifacts }
        def cordaDeployFiles = cordaDeployArtifacts*.file
        
        for(nodeDir in cordaNodes*.nodeDir) {
            copy {
                from cordaDeployFiles
                into "$nodeDir/cordapps"
            }
        }
    }
}

task deployCordappToNodes(group: RUN_CORDA_GROUP) {
    doLast {
        for(nodeDir in cordaNodes*.nodeDir) {
            copy {
                from('devops/Dockerfile-corda')
                into(nodeDir)
            }
        }
    }
}

//task killCordaProcesses(type: Exec, group: RUN_CORDA_GROUP) {
//    commandLine "bash", "-c", 'kill -9 `jps | grep "Corda" | cut -d " " -f 1` || true'
//}

task deployCordaNetwork(group: RUN_CORDA_GROUP) {
    dependsOn deployNodes
    dependsOn deployJarsToNodes
    dependsOn deployCordappToNodes
}

task runNodes(type: Exec, group: RUN_CORDA_GROUP) {
    dependsOn deployCordaNetwork

    workingDir "$rootDir/build/nodes"
    executable "./runnodes"
}
